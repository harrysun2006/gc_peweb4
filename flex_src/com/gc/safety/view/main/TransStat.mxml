<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="onCreationComplete()"
           xmlns:main="com.gc.safety.view.main.*" width="100%" height="100%">
  <mx:HBox width="100%" height="100%">
    <mx:AdvancedDataGrid id="adg" dataProvider="{dg}" labelFunction="{CommonUtil.gridLabelFunction}"
                         horizontalScrollPolicy="auto" verticalScrollPolicy="auto" width="100%"
                         height="100%" styleFunction="onStyleFunction" doubleClickEnabled="true"
                         doubleClick="onDoubleClick(adg.selectedItem)">
      <mx:columns>
        <mx:AdvancedDataGridColumn dataField="no" formatter="{Constants.NUMBER_FORMATTER_N0}"
                                   headerText="序号" sortCompareFunction="onSortCompareFunction"
                                   textAlign="center" width="64"/>
        <mx:AdvancedDataGridColumn dataField="{Constants.PROP_NAME_LABEL}"/>
        <mx:AdvancedDataGridColumn dataField="{Constants.PROP_NAME_COUNT}"
                                   formatter="{Constants.NUMBER_FORMATTER_N0}" textAlign="right"/>
        <mx:AdvancedDataGridColumn dataField="_totalLc" textAlign="right"/>
        <mx:AdvancedDataGridColumn dataField="_times" formatter="{Constants.NUMBER_FORMATTER_N0}"
                                   textAlign="right"/>
        <mx:AdvancedDataGridColumn/>
      </mx:columns>
    </mx:AdvancedDataGrid>
    <!--main:TransStatPie id="pie"/-->
  </mx:HBox>
  <mx:Script>
    <![CDATA[
      import mx.utils.ObjectUtil;
      import com.gc.CommonEvent;
      import com.gc.safety.controller.CommonController;
      import com.gc.common.model.EquOnline;
      import com.gc.Constants;
      import com.gc.util.CommonUtil;
      import mx.collections.ArrayCollection;

      [Bindable]
      private var dg:ArrayCollection=new ArrayCollection();

      private function onCreationComplete():void
      {
        CommonUtil.setDataGridColumns(adg, "gcc_safety", "tree.transStat");
        addEventListener(CommonEvent.EXPORT,function(event:CommonEvent):void 
          {
            CommonUtil.export(CommonUtil.getGridData(adg),CommonUtil.getGridHeaders(adg),null,event.data.label + "违章统计列表.xls");
          });
      }

      private function onSortCompareFunction(obj1:Object,obj2:Object):int
      {
        return ObjectUtil.numericCompare(obj1.no,obj2.no);
      }

      private function onStyleFunction(data:Object, col:AdvancedDataGridColumn):Object
      {
        if (data.hasOwnProperty("color") && data["color"] != null)
        {
          return { color:0x8E388E, fontWeight:"bold", fontSize:12 };
        }

        return {};
      }

      private function onDoubleClick(item:Object):void
      {
        if (item != null)
          parent.dispatchEvent(new CommonEvent(CommonEvent.DBLCLICKED,item));
      }

      public function statTrans(item:Object, ancestors:Array, orders:Array):void
      {
        if (item == null || !(item["children"] is Array))
          return;
        dg.removeAll();
        counter(item);
        var children:Array=item["children"] as Array;
        var i:int = 0;
        for each (var child:Object in children)
        {
          child.no = ++i;
          child.color = null;
          dg.addItem(child);
        }
        item.no = ++i;
        item.color = "";
        dg.addItem(item);

        // 饼图
//        pie.dispatchEvent(new CommonEvent(CommonEvent.REFRESH, dg));
      }

      private function counter(item:Object):void
      {
        if (item == null)
          return;
        if (item["children"] == null || !item["children"] is Array)
        {
          item[Constants.PROP_NAME_COUNT]=(item.downDate != null && item.downDate <= CommonController.now) ? 0 : 1;
          if (item is EquOnline && item["id"] == 0)
          {
            // TODO: 显示公务车,没有线路的部门,后台尚未处理
            item[Constants.PROP_NAME_COUNT]=0;
            item["_totalLc"]=0;
            item["_times"]=0;
            return;
          }
          item["_totalLc"]=item.mileage;
          item["_times"]=item.trans.length;
          return;
        }
        var children:Array=item["children"] as Array;
        var count:int=0;
        var totalLc:Number=0;
        var times:int=0;
        for each (var child:Object in children)
        {
          if (!child.hasOwnProperty(Constants.PROP_NAME_COUNT))
            counter(child);
          count+=child[Constants.PROP_NAME_COUNT];
          totalLc+=child["_totalLc"];
          times+=child["_times"];
        }
        item[Constants.PROP_NAME_COUNT]=count;
        item["_totalLc"]=totalLc;
        item["_times"]=times;
      }
    ]]>
  </mx:Script>
</mx:Canvas>
