<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:gc="com.gc.controls.*"
                preinitialize="onPreinitialize()" creationComplete="onCreationComplete()">
  <mx:VBox width="100%" height="100%">
    <mx:AdvancedDataGrid id="adg" width="100%" height="100%" horizontalScrollPolicy="auto"
                         editable="false" selectionMode="singleRow" doubleClickEnabled="true"
                         labelFunction="{CommonUtil.gridLabelFunction}" doubleClick="view(event)">
      <mx:columns>
        <mx:AdvancedDataGridColumn dataField="{Constants.PROP_NAME_SERIAL}" width="64"
                                   labelFunction="{CommonUtil.serialLabelFunction}"
                                   styleFunction="{CommonUtil.serialStyleFunction}"/>
        <mx:AdvancedDataGridColumn dataField="id" dataTipField="id.no"/>
        <mx:AdvancedDataGridColumn dataField="depart" dataTipField="depart.name"/>
        <mx:AdvancedDataGridColumn dataField="date"/>
        <mx:AdvancedDataGridColumn dataField="issueDate"/>
        <mx:AdvancedDataGridColumn dataField="issueType" dataTipField="issueType.label"/>
        <mx:AdvancedDataGridColumn dataField="summary"/>
        <mx:AdvancedDataGridColumn dataField="issuer" dataTipField="issuer.name"/>
        <mx:AdvancedDataGridColumn dataField="comment"/>
      </mx:columns>
    </mx:AdvancedDataGrid>
    <mx:HBox horizontalAlign="center" width="100%" paddingTop="7" paddingBottom="7">
      <mx:FormItem label="{resourceManager.getString('gcc_hr', 'salFact.depart')}:" width="50%">
        <mx:ComboBox id="cbDepart" width="200" itemRenderer="mx.controls.Label"
                     change="onDepartChange(event)"/>
      </mx:FormItem>
      <mx:Button label="{resourceManager.getString('gcc', 'refresh')}" click="refresh(event)"/>
      <mx:Button label="{resourceManager.getString('gcc', 'edit')}" click="edit(event)"
                 enabled="{UserController.hrSalWrite}"/>
      <mx:Button label="{resourceManager.getString('gcc', 'delete')}" click="_delete(event)"
                 enabled="{UserController.hrSalWrite}" visible="false" includeInLayout="false"/>
      <mx:Button label="{resourceManager.getString('gcc', 'close')}" click="close(event)"/>
      <mx:FormItem width="50%"/>
    </mx:HBox>
  </mx:VBox>
  <mx:Script>
    <![CDATA[
      import mx.collections.ArrayCollection;
      import mx.controls.Alert;
      import mx.controls.ComboBox;
      import mx.controls.Label;
      import mx.events.CloseEvent;
      import mx.events.FlexEvent;
      import mx.events.ListEvent;
      import mx.rpc.events.ResultEvent;
      import mx.utils.ObjectUtil;

      import com.gc.common.controller.BaseController;
      import com.gc.common.controller.UserController;
      import com.gc.common.model.Department;
      import com.gc.CommonEvent;
      import com.gc.Constants;
      import com.gc.hr.Events;
      import com.gc.hr.controller.CommonController;
      import com.gc.hr.controller.SalaryController;
      import com.gc.hr.model.SalFact;
      import com.gc.util.CommonUtil;
      import com.gc.util.DateUtil;

      private var list:ArrayCollection;

      private function onPreinitialize():void
      {
        addEventListener(Events.ADD_SALFACT, onAddSalFact);
        addEventListener(Events.DEL_SALFACT, onDelSalFact);
        addEventListener(Events.EDIT_SALFACT, onEditSalFact);
      }

      private function onCreationComplete():void
      {
        CommonUtil.setDataGridColumns(adg, "gcc_hr", "salFact");
        var departs:ArrayCollection=ObjectUtil.copy(BaseController.departmentList) as ArrayCollection;
        departs.addItemAt(new Department(0, UserController.branch, resourceManager.getString("gcc", "depart.all")), 0);
        cbDepart.dataProvider=departs;
        cbDepart.selectedIndex=-1;
        if (UserController.hrSub)
        {
          cbDepart.enabled=false;
          cbDepart.selectedIndex=CommonUtil.indexOfKey(BaseController.departmentList, UserController.hrDepart);
        }
        refresh();
      }

      private function onDepartChange(e:Event=null):void
      {
        refresh();
      }

      private function refresh(e:Event=null):void
      {
        var did:int=cbDepart.selectedItem ? cbDepart.selectedItem.id : 0;
        SalaryController.getFacts(UserController.branchId, did, function(e1:ResultEvent):void
          {
            list=e1.result as ArrayCollection;
            adg.dataProvider=list;
          });
      }

      private function add(e:Event):void
      {
        CommonUtil.popupWindow(FactAE, "", this, [new SalFact()]);
      }

      private function edit(e:Event):void
      {
        var po:SalFact=adg.selectedItem as SalFact;
        if (po == null)
        {
          Alert.show(resourceManager.getString("gcc", "edit.not.null"), Constants.APP_NAME, Alert.OK, null, null, Constants.ICON32_WARNING);
        }
        else
        {
          if (po.issueDate < CommonController.lastCloseDate)
          {
            Alert.show(resourceManager.getString("gcc_hr", "salFact.edit.error.before.closeDate", 
              [po.no, DateUtil.formatDate(po.issueDate), DateUtil.formatDate(CommonController.lastCloseDate)]),
              Constants.APP_NAME, Alert.OK, null, function(e1:Event):void{view();}, Constants.ICON32_WARNING);
          }
          CommonUtil.popupWindow(FactAE, "", this, [po]);
        }
      }

      private function _delete(e:Event):void
      {
        var po:SalFact=adg.selectedItem as SalFact;
        if (po == null)
        {
          Alert.show(resourceManager.getString("gcc", "delete.not.null"), Constants.APP_NAME, Alert.OK, null, null, Constants.ICON32_WARNING);
        }
        else
        {
          Alert.show(resourceManager.getString("gcc_hr", "salFact.del.query", [po.departName, po.no]), Constants.APP_NAME, Alert.YES | Alert.NO,
            null, function(e1:CloseEvent):void
            {
              if (e1.detail == Alert.YES)
              {
                SalaryController.deleteFact(po, function(e1:ResultEvent):void
                  {
                    Alert.show(resourceManager.getString("gcc_hr", "salFact.del1.success", [po.departName, po.no]), Constants.APP_NAME, Alert.OK, null, null, Constants.ICON32_INFO);
                    dispatchEvent(new Events(Events.DEL_SALFACT, po));
                  });
              }
            });
        }
      }

      private function view(e:Event=null):void
      {
        var po:Object=adg.selectedItem;
        if (po == null)
        {
          Alert.show(resourceManager.getString("gcc", "view.not.null"), Constants.APP_NAME, Alert.OK, null, null, Constants.ICON32_WARNING);
        }
        else
        {
          SalaryController.getFactPersonsAndItems(po, function(e1:ResultEvent):void
            {
              var persons:Object=e1.result.persons;
              var items:Object=e1.result.items;
              var head_old:Object=e1.result.head;
              var _data:Object=e1.result.data;
              var head:Object=ObjectUtil.copy(head_old);
              CommonUtil.popupWindow(FactVoucher, "", null, [persons, items, head, _data, head_old, Constants.OP_VIEW]);
            });
        }
      }

      private function close(e:Event=null):void
      {
        dispatchEvent(CommonEvent.CLOSE_EVENT);
      }

      private function onAddSalFact(e:Events):void
      {
        list.addItem(e.data);
      }

      private function onDelSalFact(e:Events):void
      {
        var k:int=CommonUtil.indexOfKey(list, e.data.no, "no");
        if (k >= 0)
          list.removeItemAt(k);
      }

      private function onEditSalFact(e:Events):void
      {
        var sf:SalFact=e.data as SalFact;
        for each (var obj:SalFact in list)
        {
          if (obj.no == sf.no)
          {
            CommonUtil.copyProperties(sf, obj);
            break;
          }
        }
      }
    ]]>
  </mx:Script>
</mx:TitleWindow>
